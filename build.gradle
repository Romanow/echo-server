plugins {
    id "idea"
    id "org.jetbrains.kotlin.jvm" version "2.2.10"
    id "org.jetbrains.kotlin.plugin.spring" version "2.2.10" apply false
    id "org.jlleitschuh.gradle.ktlint" version "14.0.1" apply false
    id "org.springframework.boot" version "3.5.9" apply false
    id "io.spring.dependency-management" version "1.1.7"
    id "com.gorylenko.gradle-git-properties" version "2.4.2" apply false
}

ext {
    springBootVersion = "3.5.9"
    springCloudVersion = "3.3.0"
    wireMockVersion = "3.10.6"
}

subprojects { ->
    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "io.spring.dependency-management"

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    ktlint {
        version.set("1.8.0")
    }

    repositories {
        mavenCentral()
    }

    [compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "21"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            mavenBom "org.springframework.cloud:spring-cloud-kubernetes-dependencies:$springCloudVersion"
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
        implementation "jakarta.validation:jakarta.validation-api"
    }

    processResources {
        filesMatching(["**/properties-from-gradle.properties"]) {
            expand(project.properties)
        }
    }
}
